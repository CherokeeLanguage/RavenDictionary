plugins {
    id "java"
    id "idea"
    id "eclipse"
    id "eclipse-wtp"
    id "com.diffplug.eclipse.apt"
    id 'com.github.johnrengelman.shadow'
}

compileJava.options.encoding = 'UTF-8'
compileJava.options.release = 17

compileTestJava.options.encoding = 'UTF-8'
compileTestJava.options.release = 17

java.toolchain.languageVersion = JavaLanguageVersion.of(17)
java.toolchain.vendor = JvmVendorSpec.ADOPTOPENJDK

jar.manifest.attributes "Main-Class": 'com.cherokeelessons.raven.Main'
shadowJar.archiveFileName = 'RavenDictionary.jar'
shadowJar.mergeServiceFiles()

repositories {
    mavenCentral()
    google()
    maven { url "https://jitpack.io/" }
}

configurations {
    provided
}

dependencies {
    implementation 'org.apache.commons:commons-csv:1.9.0'
    implementation 'org.apache.commons:commons-text:1.9'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.14.1'
    implementation 'commons-io:commons-io:2.11.0'
    implementation 'org.languagetool:language-en:5.8'
    implementation 'org.languagetool:languagetool-core:5.8'
    implementation project(':SimpleTextUi')

    testImplementation 'org.testng:testng:7.6.1'
}

sourceSets {
    main {
        compileClasspath += configurations.provided
    }
    test {
        compileClasspath += configurations.provided
        runtimeClasspath += configurations.provided
    }
}

task "create-dirs" {
    doLast {
        sourceSets*.java.srcDirs*.each { it.mkdirs() }
        sourceSets*.resources.srcDirs*.each { it.mkdirs() }
    }
}

eclipseJdt {
    doLast {
        File f = file('.settings/org.eclipse.core.resources.prefs')
        f.write('eclipse.preferences.version=1\n')
        f.append('encoding/<project>=utf-8')
        f = file('.settings/org.eclipse.core.runtime.prefs')
        f.write('eclipse.preferences.version=1\n')
        f.append('line.separator=\\n\n')
    }
}

// Required for correct setup of APT in Eclipse
eclipseJdt.dependsOn cleanEclipseJdt, eclipseJdtApt
eclipseJdtApt.dependsOn cleanEclipseJdtApt, eclipseFactorypath
eclipseFactorypath.dependsOn cleanEclipseFactorypath, "create-dirs"

eclipse {
    synchronizationTasks eclipseJdt
    project {
        name = 'RavenDictionary'
        comment = 'The Raven Dictionary Project'
    }

    classpath {
        plusConfigurations += [configurations.provided]
        downloadSources = true
        downloadJavadoc = true
    }

    jdt {
    }

    wtp {
        facet {
        }
    }
}

test {
    useTestNG()
}
